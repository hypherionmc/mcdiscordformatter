plugins {
    id 'java'
    id "xyz.wagyourtail.unimined" version "1.1.0-SNAPSHOT"
    id 'com.github.johnrengelman.shadow' version '8.0.0'
    id 'maven-publish'
}

// Edit in gradle.properties
group = project_group
version = "${version_major}.${version_minor}.${version_patch}"
archivesBaseName = "${mod_name}-${minecraft_version}"

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

configurations {
    shade
    implementation.extendsFrom shade
}

// Add your maven repositories here
repositories {
    mavenCentral()

    // First Dark Dev Maven. Can be removed
    maven {
        url "https://maven.firstdark.dev/releases"
    }

    // First Dark Dev Mirror maven. Do not remove.
    // It's required by this project to pull in dependencies for the modloaders
    maven {
        url "https://mcentral.firstdark.dev/releases"
    }
}

dependencies {
    shade group: 'dev.vankka', name: 'SimpleAST', version: '2.2.7'
}

jar {
    manifest {
        attributes([
                'Specification-Title'     : project.archivesBaseName,
                'Specification-Vendor'    : mod_author,
                'Specification-Version'   : project.version,
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.version,
                'Implementation-Vendor'   : mod_author,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Timestamp'               : System.currentTimeMillis(),
                'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                'Built-On-Minecraft'      : minecraft_version
        ])
    }
}

/**
 * ===============================================================================
 * =       DO NOT EDIT BELOW THIS LINE UNLESS YOU KNOW WHAT YOU ARE DOING        =
 * ===============================================================================
 */

unimined.minecraft {
    version minecraft_version

    fabric {
        loader fabric_loader
    }

    defaultRemapJar = false

    mappings {
        mojmap()
        devNamespace "mojmap"
    }
}

shadowJar {
    from sourceSets.main.output
    configurations = [project.configurations.shade]
    setArchiveClassifier('')

    def relocPath = "me.hypherionmc.mcdiscordformatter.reloc."
    relocate "dev.vankka", relocPath + "dev.vankka"
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = 'UTF-8'
    it.options.release = 17
}

tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

clean {
    delete "$rootDir/artifacts"
}

tasks.register('delDevJar') {
    doLast {
        def tree = fileTree('build/libs')
        tree.include '**/*-dev-shadow.jar'
        tree.include '**/*-dev.jar'
        tree.include '**/*-all.jar'
        tree.include '**/*-slim.jar'
        tree.each { it.delete() }
    }
}
build.finalizedBy delDevJar

def env = System.getenv()

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            artifact(shadowJar) {
                builtBy shadowJar
            }
        }
    }

    repositories {
        if (env.MAVEN_URL) {
            maven {
                credentials {
                    username env.MAVEN_USER
                    password env.MAVEN_PASS
                }
                url env.MAVEN_URL
            }
        } else {
            mavenLocal()
        }
    }
}

publishing.publications.remove(publishing.publications.mavenJava)